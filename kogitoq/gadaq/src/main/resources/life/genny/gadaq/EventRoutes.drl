package life.genny.gadaq;

import life.genny.qwandaq.message.QEventMessage;
import life.genny.qwandaq.message.QDataBaseEntityMessage;
import life.genny.qwandaq.models.UserToken;

import org.apache.commons.lang3.StringUtils;
import life.genny.qwandaq.utils.SearchUtils;
import life.genny.qwandaq.utils.CacheUtils;
import life.genny.qwandaq.utils.KafkaUtils;
import life.genny.qwandaq.utils.BaseEntityUtils;
import life.genny.kogito.common.utils.KogitoUtils;

import life.genny.qwandaq.entity.BaseEntity;
import life.genny.qwandaq.entity.SearchEntity;
import javax.json.bind.Jsonb;
import javax.json.Json;
import javax.json.JsonObject;
import javax.json.JsonObjectBuilder;

ruleflow-group 'EventRoutes'

rule RouteAuthInit 
when
	msg: QEventMessage( data.code == "AUTH_INIT" )
	kogitoUtils : KogitoUtils()
then
	kogitoUtils.triggerWorkflow("authInit", "eventMessage", msg);
	retract(msg);
end

rule RouteSubmit 
when
	msg: QEventMessage( data.code == "QUE_SUBMIT" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getData().getProcessId();
	kogitoUtils.sendSignal("processQuestions", processId, "submit", "");
	retract(msg);
end

rule RouteCancel 
when
	msg: QEventMessage( data.code == "QUE_CANCEL" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getData().getProcessId();
	kogitoUtils.sendSignal("processQuestions", processId, "cancel", "");
	retract(msg);
end

rule RouteReset 
when
	msg: QEventMessage( data.code == "QUE_RESET" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getData().getProcessId();
	kogitoUtils.sendSignal("processQuestions", processId, "reset", "");
	retract(msg);
end

rule RouteAddItem 
when
	msg: QEventMessage( data.code matches "QUE_ADD_.*" )
	kogitoUtils : KogitoUtils()
	userToken : UserToken()
then
	String code = StringUtils.removeStart(msg.getData().getCode(), "QUE_ADD_");

	JsonObject json = Json.createObjectBuilder()
		.add("definitionCode", "DEF_"+code)
		.add("sourceCode", userToken.getUserCode())
		.build();

	kogitoUtils.triggerWorkflow("personLifecycle", json);
	retract(msg);
end

rule RouteView 
when
	msg: QEventMessage( data.code matches "QUE_.*_VIEW" )
	kogitoUtils : KogitoUtils()
then
	kogitoUtils.triggerWorkflow("view", "eventMessage", msg);
	retract(msg);
end

/**
 * If no route exists within gadaq, the message should be
 * sent to the project specific service.
 */
rule ForwardEvent
salience 0
when
	msg: QEventMessage()
then
	System.out.println("Forwarding Event Message...");
	KafkaUtils.writeMsg("genny_events", msg);
	retract(msg);
end

