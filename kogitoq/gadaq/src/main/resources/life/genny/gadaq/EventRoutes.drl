package life.genny.gadaq;

import javax.json.Json;
import javax.json.JsonObject;
import javax.json.bind.Jsonb;

import org.apache.commons.lang3.StringUtils;

import life.genny.qwandaq.message.QEventMessage;
import life.genny.qwandaq.models.UserToken;
import life.genny.qwandaq.utils.KafkaUtils;
import life.genny.qwandaq.utils.DefUtils;
import life.genny.qwandaq.utils.GraphQLUtils;
import life.genny.kogito.common.utils.KogitoUtils;
import static life.genny.kogito.common.utils.KogitoUtils.UseService.*;
import life.genny.kogito.common.models.RLog;

ruleflow-group 'EventRoutes'

rule RouteAuthInit 
when
	msg: QEventMessage( code == "AUTH_INIT" )
	kogitoUtils : KogitoUtils()
then
	/* RLog.info(drools, "Auth Init!"); */
	kogitoUtils.triggerWorkflow(SELF, "authInit", "eventMessage", msg);
	retract(msg);
end

rule RouteSubmit 
when
	msg: QEventMessage( code == "QUE_SUBMIT" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getProcessId();
	kogitoUtils.sendSignal(SELF, "processQuestions", processId, "submit", "");
	retract(msg);
end

rule RouteCancel 
when
	msg: QEventMessage( code == "QUE_CANCEL" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getProcessId();
	kogitoUtils.sendSignal(SELF, "processQuestions", processId, "cancel", "");
	retract(msg);
end

rule RouteReset 
when
	msg: QEventMessage( code == "QUE_RESET" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getProcessId();
	kogitoUtils.sendSignal(SELF, "processQuestions", processId, "reset", "");
	retract(msg);
end

rule RouteView 
when
	msg: QEventMessage( code matches "QUE_.*_VIEW" || code == "ACT_VIEW" || code == "QUE_TREE_ITEM_HOST_COMPANIES" )
	kogitoUtils : KogitoUtils()
then
	kogitoUtils.triggerWorkflow(SELF, "view", "eventMessage", msg);
	retract(msg);
end

rule RouteAddItem 
when
	msg: QEventMessage( code matches "QUE_ADD_.*" )
	kogitoUtils : KogitoUtils()
	userToken : UserToken()
	defUtils : DefUtils()
then
	System.out.println("Routing Add Item!");
	String code = StringUtils.removeStart(msg.getCode(), "QUE_ADD_");
	String prefix = defUtils.getDefPrefix(userToken.getProductCode(), "DEF_"+code);

	System.out.println("Prefix = " + prefix);

	/* if (!"PER".equals(prefix)) { */
	/* 	return; */
	/* } */

	/* NOTE: This is very very temporary */
	if ("PROPERTY".equals(code)) {
		return;
	}

	JsonObject json = Json.createObjectBuilder()
		.add("definitionCode", "DEF_"+code)
		.add("sourceCode", userToken.getUserCode())
		.build();

	kogitoUtils.triggerWorkflow(SELF, "personLifecycle", json);
	retract(msg);
end

rule RouteEditItem 
when
	msg: QEventMessage( code == "ACT_EDIT" )
	kogitoUtils : KogitoUtils()
then
	kogitoUtils.triggerWorkflow(SELF, "edit", "eventMessage", msg);
	retract(msg);
end

rule RouteUpdateSummary 
when
	msg: QEventMessage( code == "UPDATE_SUMMARY" )
	kogitoUtils : KogitoUtils()
	gqlUtils : GraphQLUtils()
	jsonb : Jsonb()
then
	System.out.println("Updating Summary");
	String processId = gqlUtils.fetchProcessId("PersonLifecycle", "entityCode", msg.getTargetCode());
	System.out.println("PID = " + processId);
	kogitoUtils.sendSignal(SELF, "personLifecycle", processId, "update_summary", jsonb.toJson(msg));
	retract(msg);
end

rule RouteTreeItems 
when
	msg: QEventMessage( code matches "QUE_TREE_ITEM_.*"  && code != "QUE_TREE_ITEM_HOST_COMPANIES" )
	kogitoUtils : KogitoUtils()
then
    System.out.println("DEBUG LOG");
	kogitoUtils.triggerWorkflow(SELF, "view", "eventMessage", msg);
	retract(msg);
end

/**
 * If no route exists within gadaq, the message should be
 * sent to the project specific service.
 */
rule ForwardEvent
salience 0
when
	msg: QEventMessage()
then
	System.out.println("Forwarding Event Message...");
	KafkaUtils.writeMsg("genny_events", msg);
	retract(msg);
end

