package life.genny.gadaq;

import javax.json.Json;
import javax.json.JsonObject;
import javax.json.JsonObjectBuilder;
import javax.json.bind.Jsonb;

import org.apache.commons.lang3.StringUtils;

import life.genny.qwandaq.message.QEventMessage;
import life.genny.qwandaq.models.UserToken;
import life.genny.qwandaq.utils.KafkaUtils;
import life.genny.qwandaq.utils.QwandaUtils;
import life.genny.qwandaq.utils.BaseEntityUtils;
import life.genny.qwandaq.utils.DefUtils;
import life.genny.qwandaq.utils.CacheUtils;
import life.genny.qwandaq.utils.GraphQLUtils;
import life.genny.qwandaq.utils.CommonUtils;
import life.genny.kogito.common.utils.KogitoUtils;
import static life.genny.kogito.common.utils.KogitoUtils.UseService.*;
import life.genny.kogito.common.models.RLog;

ruleflow-group 'EventRoutes'

rule RouteAuthInit 
when
	msg: QEventMessage( data.code == "AUTH_INIT" )
	kogitoUtils : KogitoUtils()
then
	/* RLog.info(drools, "Auth Init!"); */
	kogitoUtils.triggerWorkflow(SELF, "authInit", "eventMessage", msg);
	retract(msg);
end

rule RouteSubmit 
when
	msg: QEventMessage( data.code == "QUE_SUBMIT" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getData().getProcessId();
	kogitoUtils.sendSignal(SELF, "processQuestions", processId, "submit", "");
	retract(msg);
end

rule RouteUpdate 
when
	msg: QEventMessage( data.code == "QUE_UPDATE" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getData().getProcessId();
	kogitoUtils.sendSignal(SELF, "processQuestions", processId, "update", "");
	retract(msg);
end

rule RouteCancel 
when
	msg: QEventMessage( data.code == "QUE_CANCEL" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getData().getProcessId();
	kogitoUtils.sendSignal(SELF, "processQuestions", processId, "cancel", "");
	retract(msg);
end

rule RouteReset 
when
	msg: QEventMessage( data.code == "QUE_RESET" )
	kogitoUtils : KogitoUtils()
then
	String processId = msg.getData().getProcessId();
	kogitoUtils.sendSignal(SELF, "processQuestions", processId, "reset", "");
	retract(msg);
end

rule RouteView 
when
	msg: QEventMessage( data.code matches "QUE_.*_VIEW" || data.code matches "QUE_TABLE_.*" || data.code == "ACT_VIEW" || data.code matches "QUE_TREE_ITEM_.*" )
	kogitoUtils : KogitoUtils()
then
    String code = CommonUtils.removePrefix(msg.getData().getCode());
    code = code.replace("_VIEW","");
    code = code.replace("TABLE_","");

	JsonObjectBuilder builder = Json.createObjectBuilder().add("code", code);

	if (msg.getData().getTargetCode() != null) 
		builder.add("targetCode", msg.getData().getTargetCode());

	kogitoUtils.triggerWorkflow(SELF, "view", builder.build());
	retract(msg);
end

rule RouteApply
salience 1000
when
	msg: QEventMessage( data.code matches "ACT_PRI_EVENT_APPLY")
	kogitoUtils : KogitoUtils()
	userToken : UserToken()
	defUtils : DefUtils()
then
	System.out.println("Forwarding Event Message..."+msg.getData().getCode());
	KafkaUtils.writeMsg("genny_events", msg);
	retract(msg);
end

rule RouteTestForm 
when
	msg: QEventMessage( data.code matches "TEST_QUE_.*")
	userToken: UserToken()
	kogitoUtils : KogitoUtils()
	beUtils: BaseEntityUtils()
	defUtils:	DefUtils()
	qwandaUtils: QwandaUtils()
then
	
	System.out.println("Displaying Test Question Group ..."+msg.getData().getCode());
	JsonObject payload = Json.createObjectBuilder()
		.add("questionCode", msg.getData().getCode().substring("TEST_".length()))
		.add("userCode", userToken.getUserCode())
		.add("sourceCode", userToken.getUserCode())
		.add("targetCode", msg.getData().getTargetCode())
		.build();

	kogitoUtils.triggerWorkflow(SELF,"testQuestion", payload);
	retract(msg);
end

rule RouteAddItem 
when
	msg: QEventMessage( data.code matches "QUE_ADD_.*" )
	kogitoUtils : KogitoUtils()
	userToken : UserToken()
	defUtils : DefUtils()
then
	System.out.println("Routing Add Item!");
	String code = StringUtils.removeStart(msg.getData().getCode(), "QUE_ADD_");
	String prefix = CacheUtils.getObject(userToken.getProductCode(), "DEF_"+code+":PREFIX", String.class);
	System.out.println("Prefix = " + prefix);

	if (!"PER".equals(prefix))
		return;

	JsonObject json = Json.createObjectBuilder()
		.add("definitionCode", "DEF_"+code)
		.add("sourceCode", userToken.getUserCode())
		.build();

	kogitoUtils.triggerWorkflow(SELF, "personLifecycle", json);
	retract(msg);
end

rule RouteEditItem 
when
	msg: QEventMessage( data.code == "ACT_EDIT" )
	kogitoUtils : KogitoUtils()
then
	kogitoUtils.triggerWorkflow(SELF, "edit", "eventMessage", msg);
	retract(msg);
end

rule RouteUpdateSummary 
when
	msg: QEventMessage( data.code == "UPDATE_SUMMARY" )
	kogitoUtils : KogitoUtils()
	gqlUtils : GraphQLUtils()
	jsonb : Jsonb()
then
	System.out.println("Updating Summary");
	String processId = gqlUtils.fetchProcessId("PersonLifecycle", "entityCode", msg.getData().getTargetCode());
	System.out.println("PID = " + processId);
	kogitoUtils.sendSignal(SELF, "personLifecycle", processId, "update_summary", jsonb.toJson(msg));
	retract(msg);
end

rule RouteTreeItems 
when
	msg: QEventMessage( data.code matches "QUE_TREE_ITEM_.*"  && data.code != "QUE_TREE_ITEM_HOST_COMPANIES" )
	kogitoUtils : KogitoUtils()
then
    System.out.println("DEBUG LOG");
	kogitoUtils.triggerWorkflow(SELF, "view", "eventMessage", msg);
	retract(msg);
end


/**
 * If no route exists within gadaq, the message should be
 * sent to the project specific service.
 */
rule ForwardEvent
salience 0
when
	msg: QEventMessage()
then
	System.out.println("Forwarding Event Message...");
	KafkaUtils.writeMsg("genny_events", msg);
	retract(msg);
end

